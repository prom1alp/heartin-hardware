#include        "msp430x54xA.h"
#include        "register.h"
#include        "adc.h"
;________________________________________________________________________________________________________________
EXTERN          FilterBuffer
;________________________________________________________________________________________________________________
EXPORT          MakeFilter
;################################################################################################################
                RSEG                  CODE                            ;сегмент кода программы
;________________________________________________________________________________________________________________
MakeFilter      ;фильтрация данных
;REG1 - указатель на данные
;REG2 - указатель на выходной буфер
;REG3 - указатель на коэффициенты фильтра
;REG4 - размер фильтра
                clr.w                 REG5                            ;номер канала
                mov.w                 #FilterBuffer,REG6              ;граница буфера
                mov.w                 #FilterSampleSize+2,REG7        ;шаг по каналам (2 добавляется при взятии LB)
MakeFilterByCh  mov.w                 #0,&MPYS                        ;выполняем перемножение для сброса аккумулятора
                mov.w                 #0,&OP2
                mov.w                 REG1,REG8                       ;указатель на данные
                add.w                 REG5,REG8                       ;добавляем номер канала (4 байта на канал)
                add.w                 REG5,REG8
                add.w                 REG5,REG8
                add.w                 REG5,REG8
                mov.w                 REG3,REG9                       ;указатель на коэффициенты 
                mov.w                 REG4,REG10                      ;размер фильтра
FilterStage     mov.w                 @REG9+,&MACS                    ;коэффициент фильтра
                mov.w                 @REG8+,&OP2L                    ;данные LB
                mov.w                 @REG8,&OP2H                     ;данные HB
                sub.w                 REG7,REG8                       ;следующий отсчет
                cmp.w                 REG6,REG8                       ;проверка на выход за границе буфера
                jhs                   NoFilterBufBrd
                add.w                 #FilterBuffSize,REG8
NoFilterBufBrd  dec.w                 REG10
                jnz                   FilterStage
                mov.w                 &RES1,REG8                      ;читаем рузультат LB
                swpb                  REG8
                mov.b                 REG8,0(REG2)                    ;младший байт результата
                mov.w                 &RES2,REG8                      ;читаем рузультат HB
                mov.b                 REG8,1(REG2)                    ;средний байт результата
                swpb                  REG8
                mov.b                 REG8,2(REG2)                    ;старший байт результата
                add.w                 #3,REG2                         ;инкремент указателя на выходные данные
                inc.w                 REG5                            ;инкремент номера канала
                cmp.w                 #8,REG5
                jlo                   MakeFilterByCh  
                ret
;################################################################################################################
                END
